#version 430

#define VISIBLE_LIGHT_INDICES_BUFFER_GUARD -1
#define MAX_LIGHTS_PER_TILE 1024
#define BLOCK_SIZE 32

in vec4 v_FragmentPosition;
in vec3 v_Normal;

uniform int u_NumberOfTilesX;

uniform vec3 u_CameraPosition = vec3(0.0, 0.0, -2.0);

struct Light
{
	vec4 positionAndRadius;
	vec4 color;
};

layout (binding = 2, std430) readonly buffer LightsBuffer
{
    Light lights[];
};

layout(std430, binding = 3) readonly buffer VisibleLightIndicesBuffer
{
	uint visibleLightIndices[];
};

layout (location = 0) out vec4 out_Color;

void main()
{
	vec3 color = vec3(0.0);

	vec3 viewDirection = normalize(u_CameraPosition - v_FragmentPosition.xyz);
	
	uint tileIndex = int(gl_FragCoord.x / float(BLOCK_SIZE)) + int(gl_FragCoord.y / float(BLOCK_SIZE)) * u_NumberOfTilesX;
	uint startOffset = MAX_LIGHTS_PER_TILE * tileIndex;
	for (uint i = 0; i < MAX_LIGHTS_PER_TILE && visibleLightIndices[startOffset + i] != VISIBLE_LIGHT_INDICES_BUFFER_GUARD; ++i)
	{
		uint lightIndex = visibleLightIndices[startOffset + i];
		Light light = lights[lightIndex];
		
		vec3 lightDirection = light.positionAndRadius.xyz - v_FragmentPosition.xyz;
		float lightAttenuation = 1.0 - smoothstep(light.positionAndRadius.w * 0.75, light.positionAndRadius.w, length(lightDirection));
		lightDirection = normalize(lightDirection);
		
		float normalDotLightDirection = max(dot(v_Normal, lightDirection), 0.0);
		vec3 halfwayVector = normalize(lightDirection + viewDirection);
		float normalDotHalfwayVector = dot(v_Normal, halfwayVector);
		
		vec3 diffuse = normalDotLightDirection * light.color.rgb;
		vec3 specular = pow(max(normalDotHalfwayVector, 0.0), 32.0) * vec3(0.3);

		diffuse *= lightAttenuation;
		specular *= lightAttenuation;

		color.rgb += diffuse + specular;
	}
	
	vec3 ambient = vec3(0.1);
	out_Color = vec4(color + ambient, 1.0);
}